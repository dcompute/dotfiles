# Add Git working branch to prompt
autoload -Uz vcs_info
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
setopt prompt_subst
zstyle ':vcs_info:*' enable git hg svn
zstyle ':vcs_info:git*:*' get-revision true
zstyle ':vcs_info:git*:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr "+"
zstyle ':vcs_info:*' unstagedstr "!"
# zstyle ':vcs_info:*' formats " %F{15}on %F{62}%b%m %F{33}[%c%u]"
# zstyle ':vcs_info:*' actionformats " %F{15}on %F{62}%b%m %F{33}[%c%u]"

zstyle ':vcs_info:*' actionformats  " %F{1}%a %F{15}at %F{62}%12.12i %F{33}%c%u"
zstyle ':vcs_info:*' formats        " %F{15}on %F{62}%b %F{33}%c%u"

# Shell prompt based on the Solarized Dark theme.
if [[ $COLORTERM = gnome-* && $TERM = xterm ]] && infocmp gnome-256color >/dev/null 2>&1; then
  export TERM='gnome-256color';
elif infocmp xterm-256color >/dev/null 2>&1; then
  export TERM='xterm-256color';
fi;

# Highlight the user name when logged in as root.
if [[ "${USER}" == "root" ]]; then
  userStyle="%F{124}";
else
  userStyle="%F{166}";
fi;

# Highlight the hostname when connected via SSH.
if [[ "${SSH_TTY}" ]]; then
  hostStyle="%F{124}";
else
  hostStyle="%F{136}";
fi;

PS1="
%B${userStyle}%n %F{15}at ${hostStyle}%m %F{15}in %F{64}%d\${vcs_info_msg_0_}
%F{15}%# %F{reset}%b"